package compiler;

import java_cup.runtime.*;
import java.util.ArrayList;

parser code {:

    /* Erros de sintaxe não fatais */
    public void report_error(String message, Object info) {
        StringBuilder m = new StringBuilder("Erro Sintático");

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            if (s.left >= 0) {
                m.append(" na linha " + (s.left+1));
                if (s.right >= 0)
                    m.append(", coluna " + (s.right+1));
            }
            if (s.value != null) {
                m.append(". Token inesperado: " + s.value);
            }
        }
        m.append(" : " + message);
        System.err.println(m);
    }

    /* Erros de sintaxe fatais */
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

/* --- Terminais --- */
terminal CLASS, ELSE, FI, IF, IN, INHERITS, ISVOID, LET, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, NEW, OF, NOT;
terminal Boolean TRUE, FALSE;
terminal String ID, TYPEID;
terminal String STR_CONST;
terminal Integer INT_CONST;
terminal ASSIGN, DARROW, LE, LT, EQ;
terminal PLUS, MINUS, MULT, DIV, NEG, AT, DOT;
terminal LPAREN, RPAREN, LBRACE, RBRACE, COLON, SEMI, COMMA;

/* --- Não-Terminais --- */
/* Declarados antes das regras para evitar erros de "Symbol not found" */
non terminal program;
non terminal class_list, class_decl;
non terminal feature_list, feature;
non terminal formal_list, formal;
non terminal expr_list, expr;
non terminal case_list, case_branch;
non terminal argument_list;
non terminal let_list;

/* --- Precedência de Operadores (da menor para a maior precedência) --- */
precedence right ASSIGN;
precedence left NOT;
precedence nonassoc LE, LT, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;    // ~
precedence left AT;     // @
precedence left DOT;

/* --- Regras de Produção da Gramática --- */

start with program;

program ::= class_list
    {: System.out.println("=== Análise Sintática Concluída com Sucesso! ==="); :}
    ;

class_list ::= class_decl
    | class_list class_decl
    ;

class_decl ::= CLASS TYPEID:n LBRACE feature_list RBRACE SEMI
    {: System.out.println("[CLASSE] Definida: " + n); :}
    | CLASS TYPEID:n INHERITS TYPEID:p LBRACE feature_list RBRACE SEMI
    {: System.out.println("[CLASSE] Definida: " + n + " herda de " + p); :}
    ;

feature_list ::= /* vazio */
    | feature_list feature
    ;

feature ::=
      ID:name LPAREN formal_list RPAREN COLON TYPEID:type LBRACE expr RBRACE SEMI
      {: System.out.println(" --- Método: " + name + " retorna " + type); :}
    | ID:name COLON TYPEID:type ASSIGN expr SEMI
      {: System.out.println(" --- Atributo: " + name + " : " + type); :}
    | ID:name COLON TYPEID:type SEMI
      {: System.out.println(" --- Atributo: " + name + " : " + type); :}
    ;

formal_list ::= /* vazio */
    | formal
    | formal_list COMMA formal
    ;

formal ::= ID:name COLON TYPEID:type
    {: System.out.println("    (Parametro formal: " + name + " : " + type + ")"); :}
    ;

expr_list ::= expr SEMI
    | expr_list expr SEMI
    ;

case_list ::= case_branch
    | case_list case_branch
    ;

case_branch ::= ID:id COLON TYPEID:type DARROW expr SEMI
    {: System.out.println("    |-- Case Branch: " + id + " como " + type); :}
    ;

argument_list ::= expr
    | argument_list COMMA expr
    ;

let_list ::= ID:id COLON TYPEID:type
    {: System.out.println("    |-- Var Let: " + id + " : " + type); :}

    | ID:id COLON TYPEID:type ASSIGN expr
    {: System.out.println("    |-- Var Let (com init): " + id); :}

    | let_list COMMA ID:id COLON TYPEID:type
    {: System.out.println("    |-- Var Let: " + id); :}
    | let_list COMMA ID:id COLON TYPEID:type ASSIGN expr
    {: System.out.println("    |-- Var Let (com init): " + id); :}
    ;

expr ::=
      ID:id ASSIGN expr
      {: System.out.println("    |-- Atribuicao para variavel: " + id); :}
    | expr AT TYPEID:type DOT ID:id LPAREN RPAREN
    {: System.out.println("    |-- Static Dispatch @" + type + "." + id + "()"); :}
    | expr AT TYPEID:type DOT ID:id LPAREN argument_list RPAREN
    {: System.out.println("    |-- Static Dispatch @" + type + "." + id + "(...)"); :}
    | expr DOT ID:id LPAREN RPAREN
    {: System.out.println("    |-- Dispatch para metodo: " + id + "()"); :}
    | expr DOT ID:id LPAREN argument_list RPAREN
    {: System.out.println("    |-- Dispatch para metodo: " + id + "(args...)"); :}
    | ID:id LPAREN RPAREN
    {: System.out.println("    |-- Chamada Self: " + id + "()"); :}
    | ID:id LPAREN argument_list RPAREN
    {: System.out.println("    |-- Chamada Self: " + id + "(args...)"); :}
    | IF expr THEN expr ELSE expr FI
    {: System.out.println("    |-- Estrutura Condicional (IF-THEN-ELSE)"); :}
    | WHILE expr LOOP expr POOL
    {: System.out.println("    |-- Estrutura de Repeticao (WHILE)"); :}
    | LBRACE expr_list RBRACE
    {: System.out.println("    |-- Bloco de codigo { ... }"); :}
    | LET ID:id COLON TYPEID IN expr
    {: System.out.println("    |-- Estrutura LET simples (" + id + ")"); :}
    | LET ID:id COLON TYPEID ASSIGN expr IN expr
    {: System.out.println("    |-- Estrutura LET com init (" + id + ")"); :}
    | LET ID:id COLON TYPEID COMMA let_list IN expr
    {: System.out.println("    |-- Estrutura LET multiplo (" + id + ", ...)"); :}
    | CASE expr OF case_list ESAC
    {: System.out.println("    |-- Estrutura CASE"); :}
    | NEW TYPEID:type
    {: System.out.println("    |-- New (Instancia de " + type + ")"); :}
    | ISVOID expr
    {: System.out.println("    |-- Teste ISVOID"); :}
    | NEG expr
    {: System.out.println("    |-- Operacao de Negacao (~)"); :}
    | NOT expr
    {: System.out.println("    |-- Operacao NOT"); :}
    | expr PLUS expr
    {: System.out.println("    |-- Operacao SOMA (+)"); :}
    | expr MINUS expr
    {: System.out.println("    |-- Operacao SUBTRACAO (-)"); :}
    | expr MULT expr
    {: System.out.println("    |-- Operacao MULTIPLICACAO (*)"); :}
    | expr DIV expr
    {: System.out.println("    |-- Operacao DIVISAO (/)"); :}
    | expr LT expr
    {: System.out.println("    |-- Comparacao MENOR QUE (<)"); :}
    | expr LE expr
    {: System.out.println("    |-- Comparacao MENOR IGUAL (<=)"); :}
    | expr EQ expr
    {: System.out.println("    |-- Comparacao IGUAL (=)"); :}
    | LPAREN expr RPAREN
    | ID:id
    {: System.out.println("    |-- ID lido: " + id); :}
    | INT_CONST:val
    {: System.out.println("    |-- Inteiro lido: " + val); :}
    | STR_CONST:s
    {: System.out.println("    |-- String lida: \"" + s + "\""); :}
    | TRUE
    {: System.out.println("    |-- Booleano: true"); :}
    | FALSE
    {: System.out.println("    |-- Booleano: false"); :}
    ;